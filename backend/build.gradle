buildscript {
    repositories {
        for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
            maven { url(mavenRepositoryUrl) }
        }
    }
}
version = project.rootProject.ext.constructVersion()

def imageNameWithTag = "${project.rootProject.getName()}-${project.getName()}:${project.getVersion()}"
task buildDockerImage(type: Exec) {
    def dockerBuildDirectory = project.file("${project.buildDir}/runtime/docker")
    doFirst {
        dockerBuildDirectory.parentFile.mkdirs()
        copy {
            from project.file("docker")
            into dockerBuildDirectory
        }
        copy {
            from project.file("data").getAbsolutePath()
            into "${dockerBuildDirectory}/dist/data"
        }
        copy {
            from project.file("src").getAbsolutePath()
            into "${dockerBuildDirectory}/dist/src"
        }
        println("running command: ${String.join(" ", getCommandLine())}")
    }
    executable("docker")
    args(
            "build", dockerBuildDirectory.getAbsolutePath(),
            "-f", project.file("${dockerBuildDirectory}/Dockerfile").getAbsolutePath(),
            "-t", imageNameWithTag,
    )
}
task runDockerImage(type: Exec) {
    executable("docker")
    args(
            "run",
            "-p", "8889:80",
            "-v", "${project.file("docker/nginx.debug.conf").getAbsolutePath()}:/etc/nginx/conf.d/default.conf",
            imageNameWithTag,
    )
    dependsOn(buildDockerImage)
}
